plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.109'
}

version = mod_version
group = mod_group_id

base {
    archivesName = "${mod_name}-${minecraft_version}"
}

// Mojang ships Java 21 to end users starting in 1.20.5.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
}
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version    : minecraft_version,
            neo_version          : neo_version,
            mod_license          : mod_license,
            mod_id               : mod_id,
            mod_group_id         : mod_group_id,
            mod_name             : mod_name,
            mod_version          : mod_version,
            mod_authors          : mod_authors,
            mod_display_url      : mod_display_url,
            mod_update_url       : mod_update_url,
            mod_description      : mod_description,
            mod_description_short: mod_description_short,
            pack_format          : pack_format,
            pack_format_min      : pack_format_min,
            pack_format_max      : pack_format_max,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'META-INF/mods.toml', 'fabric.mod.json', 'pack.mcmeta', 'quilt.mod.json']) {
        expand replaceProperties
    }
}

// excluding generated resources cache
jar {
    exclude '**/.cache/**'
}

// excluding class files to prevent loader issues (for example if you use Data Generators, comment this out if the data generator doesn't work)
sourceSets {
    main {
        java {
            exclude 'net/sashiro/**'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
